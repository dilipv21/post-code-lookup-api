buildscript {
    repositories {
        mavenCentral()
    }
    ext {
        springRestDocs = '1.2.1.RELEASE'
    }
    dependencies{
        classpath 'org.jruby:jruby-complete:9.1.13.0'
    }
}

apply plugin: 'org.asciidoctor.convert'

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    dependsOn test
    inputs.dir snippetsDir
}

tasks.withType(Jar) {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

dependencies {
    // spring-restdocs-core with specific version is needed to ensure correct version is used.
    testCompile("org.springframework.restdocs:spring-restdocs-core:${springRestDocs}")
    testCompile("org.springframework.restdocs:spring-restdocs-mockmvc:${springRestDocs}")
    asciidoctor("org.springframework.restdocs:spring-restdocs-asciidoctor:${springRestDocs}")
}
